{-----------------------------------------------------------------------+
|                                                                       |
|       Hello World                                                     |
|                                                                       |
+-----------------------------------------------------------------------}

gcl0x

[def {ClearScreen -- Clear screen from current position to bottom right}
  Pos= p=
  Pos 255| 255^         {Clear low byte}
  i= $8001 i+ i=        {Offset to move to next stripe}
  [do
    {Clear one stripe}
    p [do BgColor p. $100 p+ p= if>=0loop]
    {Next stripe}
    i+ p=
    255& 160^ if<>0loop]
  ret
] ClearScreen=

{ Print a 5x8 character on screen with the built-in font
  Char must be in the 32-127 range (this is not checked) }
[def {PrintChar}
  {Map ASCII code to offset in font table}
  Char 82- [if<0 50+ i= \font32up
            else     i= \font82up] fontData= {Select low or high page}
  i 2<< i+             {Multiply by 5}
  fontData+ fontData=  {Add to page address to reach bitmap data for Char}

  {Draw 5 vertical slices: 5 using font data}
  BgColor \sysArgs0.
  Color   \sysArgs1.
  Pos     \sysArgs4=
  6+      Pos=
  \SYS_VDrawBits_134 \sysFn=
  $fb i= [do
    fontData 0? fontData<++ \sysArgs2. 134!
    \sysArgs4<++
    i<++ i if<>0loop]
  ret
] PrintChar=

[def {PrintText}
  push
  Text=
  [do
    Text, Char= {Next character to be printed}
    if<>0       {Zero termination}
      Text<++   {Advance text pointer}
      Char 10^ [if=0 Pos<. $800 Pos+ Pos=
                else PrintChar!]
      loop]
  pop ret
] PrintText=

[def
   $48# $65# $6c# $6c# $6f# $20# $57# $6f# $72# $6c# $64# 0# {Hello World}
] StringHelloWorld=


{ Clear the screen and print 'Hello World' }
63 {White} Color=
0 {Black} BgColor=
$800 ClearScreen!
$800 Pos=
StringHelloWorld PrintText!
[do loop] { Loop forever }